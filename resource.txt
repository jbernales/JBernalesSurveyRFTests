*** Variables ***
${BROWSER}	Firefox
${DELAY}	0.2
${FORM URL}	https://docs.google.com/forms/d/12f7orObPipIDCZOOQsIH9_U6kGgEVOGFhd5qgO0myEo/viewform?fbzx=-5294445733973195140


*** Keywords ***
Open Browser To Test Page
	Open Browser    ${FORM URL}    ${BROWSER}
	Set Selenium Speed	${DELAY}
	Maximize Browser Window
	Test Form Should Be Open

Test Form Should Be Open
	Wait Until Page Contains Element	continue
	
Verify Text Display
	[Arguments]	${text}
	Page Should Contain	${text}

Verify Name Input Field Display
	Page Should Contain Textfield	entry.1563791636

Verify Age Input Field Display
	Page Should Contain Element	entry.930637752
	
Verify Gender Radio Buttons Display
	Page Should Contain Radio Button	Male
	Page Should Contain Radio Button	Female

Verify Sport Checkbox Display
	[Arguments]	${sport name}
	Page Should Contain Checkbox	${sport name}
	
Enter Name
	[Arguments]	${name}
    Clear Element Text  entry.1563791636
	Input Text          entry.1563791636	${name}

Enter Age
	[Arguments]	${age}
    Clear Element Text  entry.930637752
    Input Text          entry.930637752	${age}

Select Gender
	[Arguments]	${gender}
	Select Radio Button	entry.351010933	${gender}

Select Sport
	[Arguments]	${sport}
	Select Checkbox	${sport}

Continue to Next Page
	Click Button    continue

Back to First Page
	Click Button    back
	Wait Until Page Contains	Gender

Click Submit
	Click Button	submit

Click Submit Another Response
    Click Link  Magsumite ng iba pang tugon

Submit Response
    [Arguments]	${name}	${age}	${gender}	${sport}
    Enter Name	${name}
    Enter Age	${age}
    Select Gender	${gender}
    Continue to Next Page
    Select Sport	${sport}
    Click Submit

Submit Response Is Successful
    [Arguments]	${name}	${age}	${gender}	${sport}
    Submit Response     ${name}	${age}	${gender}	${sport}
    Response Is Successfully Recorded
    Click Submit Another Response

Response Is Successfully Recorded
	Page Should Contain	Your response has been recorded